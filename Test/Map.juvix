module Test.Map;

open import Stdlib.Prelude;
open import Test.JuvixUnit;

import Data.Map as Map;
open Map using {FiniteMap};

tests : List Test;
tests :=
  let
    m : FiniteMap Nat String;
    m :=
      Map.bind 2 "two" (Map.bind 1 "one" (Map.empty NatTraits.Ord));
    m2 : FiniteMap Nat Nat;
    m2 := Map.bind 3 4 (Map.bind 1 2 (Map.empty NatTraits.Ord));
    assertEqListPair :
        List (Nat × Nat) -> List (Nat × Nat) -> Assertion;
    assertEqListPair actual expected :=
      assertEqual
        (ListTraits.Eq (ProductTraits.Eq NatTraits.Eq NatTraits.Eq))
        "lists are not equal"
        (quickSort (ProductTraits.Ord NatTraits.Ord NatTraits.Ord) actual)
        expected;
  in testCase
      "Map.lookup missing key"
      (assertNothing
        (const "found a key expected to be missing")
        (Map.lookup 10 m))
    :: testCase
      "Map.lookup available key"
      (assertJust "could not find expected key" (Map.lookup 2 m))
    :: testCase
      "Map.length computes the number of keys in the map"
      (assertEqual NatTraits.Eq "expected length 2" (Map.length m) 2)
    :: testCase
      "Map.to-list computes the expected members"
      (assertEqListPair
        (Map.to-list m2)
        ((1, 2) :: (3, 4) :: nil))
    :: testCase
      "Map.bind-with replaces duplicates using merge function"
      (assertEqListPair
        (Map.to-list (Map.bind-with (+) 1 3 m2))
        ((1, 5) :: (3, 4) :: nil))
    :: testCase
      "Map.from-list-with de-duplicates using merge function"
      (assertEqListPair
        (Map.to-list
          (Map.from-list-with NatTraits.Ord (+) ((1, 1) :: (1, 2) :: nil)))
        ((1, 3) :: nil))
    :: nil;

main : IO;
main := runTestSuite (testSuite "Map" tests);
