module Data.Set;

open import Data.Ord;
open import Stdlib.Prelude hiding {length};
open import Data.Eq;
open import Test.JuvixUnit;

import Stdlib.Data.Nat.Ord as Nat;
import Stdlib.Data.Ord as Ord;

import Data.Tree as Tree;
open Tree using {BinaryTree;binaryTree;leaf;node};

type UnbalancedSet (A : Type) :=
  | unbalancedSet : Ord A
      -> BinaryTree A
      -> UnbalancedSet A;

Set : Type -> Type;
Set A := UnbalancedSet A;

empty : {A : Type} -> Ord A -> UnbalancedSet A;
empty o := unbalancedSet o leaf;

member? : {A : Type} -> A -> UnbalancedSet A -> Bool;
member? {A} x (unbalancedSet o t) :=
  let
    go : BinaryTree A -> Bool;
    go leaf := false;
    go (node l y r) :=
      if
        (ordering-lt o x y)
        (go l)
        (if (ordering-lt o y x) (go r) true);
  in go t;

insert :
  {A : Type} -> A -> UnbalancedSet A -> UnbalancedSet A;
insert {A} x (unbalancedSet o@(ord cmp) t) :=
  let
    go : BinaryTree A -> BinaryTree A;
    go leaf := node leaf x leaf;
    go n@(node l y r) :=
      case cmp x y
        | Ord.LT := node (go l) y r
        | Ord.EQ := n
        | Ord.GT := node l y (go r);
  in unbalancedSet o (go t);

length : {A : Type} -> UnbalancedSet A -> Nat;
length (unbalancedSet _ t) := Tree.length t;

to-list : {A : Type} -> UnbalancedSet A -> List A;
to-list (unbalancedSet _ t) := Tree.to-list t;

set-ordering : {A : Type} -> Ord A -> Ord (Set A);
set-ordering {A} o :=
  let
    from-ord-list : Ord (List A) -> Ord (Set A);
    from-ord-list (ord cmp) :=
      ord
        λ {
          | s1 s2 := cmp (to-list s1) (to-list s2)
        };
  in from-ord-list (list-ordering o);

printNatListLn : List Nat → IO;
printNatListLn nil := printStringLn "nil";
printNatListLn (x :: xs) :=
  printNat x >> printString " :: " >> printNatListLn xs;

tests : List Test;
tests :=
  let
    s : UnbalancedSet Nat;
    s :=
      insert
        1
        (insert 3 (insert 2 (insert 1 (empty nat-ordering))));
    setInSet : Set (Set Nat);
    setInSet := insert s (empty (set-ordering nat-ordering));
  in testCase
      "uset-size computes the expected size"
      (assertEqual nat-eq "unexpected size" (length s) 3)
    :: testCase
      "uset-members computes the expected members"
      (assertEqual
        (list-eq nat-eq)
        "unexpected memebrs"
        (quickSort nat-ordering (to-list s))
        (1 :: 2 :: 3 :: nil))
    :: testCase
      "uset-member? computes true for expected member"
      (assertTrue "expected member is not present" (member? 1 s))
    :: testCase
      "uset-member? computes false for unexpected member"
      (assertFalse "unexpected member is present" (member? 0 s))
    :: testCase
      "setInSet has length 1"
      (assertEqual nat-eq "unexpected size" (length setInSet) 1)
    :: nil;

main : IO;
main := runTestSuite (testSuite "Set" tests);
