--- N-Ary trees with pretty printing.
module Data.Tree;

open import Stdlib.Prelude;
open import Data.Showing;
open import Data.Tmp;

--- A ;List; of trees.
Forest : Type -> Type;

--- N-Ary tree.
positive
type Tree (A : Type) :=
  | node : A -> Forest A -> Tree A;

Forest A := List (Tree A);

shift :
  Nat -> String -> String -> List String -> List String;
shift n first other :=
  zipWith (++str) (first :: replicate n other);

draw : {A : Type} -> Showing A -> Tree A -> List String;

drawForest :
  {A : Type} -> Showing A -> Forest A -> List String;

draw {A} s@(showing show) (node v cs) :=
  let
    go : Tree A -> List String;
    go := draw s;
  in show v :: drawForest s cs;

drawForest s nil := nil;
drawForest s (h :: nil) :=
  "|" :: shift 300 "`- " "   " (draw s h);
drawForest s (h :: hs) :=
  "|" :: shift 300 "+- " "|  " (draw s h) ++ drawForest s hs;

treeToString : {A : Type} -> Showing A -> Tree A -> String;
treeToString s := unlines ∘ draw s;

forestToString : {A : Type} -> Showing A -> Forest A -> String;
forestToString s := unlines ∘ drawForest s;
